plugins {
    id 'java-gradle-plugin'
    id 'groovy'

    id 'eclipse'
    id 'idea'

    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.9.5'

    id 'net.minecrell.licenser' version '0.2.1'
}

group = 'net.minecrell'
version = '0.8.1'
description = 'A Gradle plugin to manage patches for Git repositories'

sourceCompatibility = 1.6
targetCompatibility = 1.6

dependencies {
    compile localGroovy()
}

license {
    header = file('LICENSE')
    include '**/*.java'
    include '**/*.groovy'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar

            repositories {
                maven {
                    url = System.getenv('REPO_' + (version.endsWith('-SNAPSHOT') ? 'SNAPSHOTS' : 'RELEASES')) ?: "$buildDir/repo"
                }
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/Minecrell/gitpatcher'
    vcsUrl = website
    description = project.description
    tags = ['git']

    plugins {
        gitpatcherPlugin {
            id = 'net.minecrell.gitpatcher'
            displayName = 'gitpatcher'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

plugins {
    id 'java-gradle-plugin'
    id 'groovy'

    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.15.0'

    id 'org.cadixdev.licenser' version '0.6.1'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'org.cadixdev.gradle'
version = '0.10.0-SNAPSHOT'
description = 'A Gradle plugin to manage patches for Git repositories'

dependencies {
    implementation gradleApi()
}

license {
    include '**/*.java'
    include '**/*.groovy'
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.named('javadocJar', Jar).configure {
    dependsOn 'groovydoc'
    archiveClassifier = 'groovydoc'
    from groovydoc.destinationDir
}

gradlePlugin {
    plugins {
        gitpatcher {
            id = 'org.cadixdev.gitpatcher'
            implementationClass = 'org.cadixdev.gradle.gitpatcher.GitPatcher'
        }
    }
}
validatePlugins {
    failOnWarning = false // abstract classes should be ok without annotations?
}

pluginBundle {
    website = 'https://github.com/CadixDev/gitpatcher'
    vcsUrl = website
    description = project.description
    tags = ['git']

    plugins {
        gitpatcherPlugin {
            id = 'org.cadixdev.gitpatcher'
            displayName = 'gitpatcher'
        }
    }
}
